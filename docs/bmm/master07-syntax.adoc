= BMM Syntax

== Overview

A BMM schema is normally written in the {openehr_odin}[ODIN syntax], although any other serialisation supporting typed object models may be used, including JSON (with type markers), YAML, XML etc. The ODIN form is described here. The structures are direct ODIN serialisations of the `P_BMM_XXX` classes in the `persistence` package.

== Header Items

The following shows the header items of a BMM schema, which correspond to the 'persistent' attributes of the class `P_BMM_SCHEMA`.

[source, odin]
--------
------------------------------------------------------
-- BMM version on which these schemas are based.
------------------------------------------------------
bmm_version = <"2.3">


------------------------------------------------------
-- schema identification
-- (schema_id computed as <rm_publisher>_<schema_name>_<rm_release>)
------------------------------------------------------
rm_publisher = <"openehr">
schema_name = <"adltest">
rm_release = <"1.0.2">

------------------------------------------------------
-- schema documentation
------------------------------------------------------
schema_revision = <"1.0.36">
schema_lifecycle_state = <"stable">
schema_description = <"openEHR schema to support test archetypes">

--------

== Inclusions

[source, odin]
--------
includes = <
    ["1"] = <
        id = <"openehr_basic_types_1.0.2">
    >
>
--------

== Archetyping related

Three archetyping related attributes can be set below, but would not usually all in the same schema. Only the archetype_rm_closure_packages variable is needed in at least one schema, usually the top-most one. 

`archetype_parent_class` defines a base class from the Reference Model that provides archetyping capability in RM data structures. It is optional, and there need be no such class in the RM. In the openEHR and 13606 RMs, this class exists, (`LOCATABLE`, and `RECORD_COMPONENT` respectively). Defining it here has the effect in tools that the class tree under which archetypes are arranged contains only RM classes inheriting from this class, e.g. `LOCATABLE` classes in the case of openEHR. If nothing is set here, all classes in the RM are assumed as candidates for archetypes. This attribute, if defined, must be defined in the same schema that defines the referenced class.                                                                     

`archetype_data_value_parent_class` defines a base class from the Reference Model whose descendants are considered to be 'logical data types' (i.e. of some higher level than the built-in primitive types String, Integer etc). This attribute is optional, even if the RM does have such a class, and is only used to help tooling provide more intelligent display, e.g. in statistical reports.                        

This attribute, if defined, must be defined in the same schema that defines the referenced class.

If `archetype_parent_class` is not set, designate a class whose descendants should be made visible in tree and grid renderings of the archetype definition. For openEHR and CEN this class is normally the same as the archetype_parent_class, i.e. `LOCATABLE` and `RECORD_COMPONENT` respectively. It is typically set for CEN, because `archetype_parent_class` may not be stated, due to demographic types not inheriting from it. The effect of this attribute in visualisation is to generate the most natural tree or grid-based view of an archetype definition, from the semantic viewpoint.

`archetype_rm_closure_packages` defines the package or packages that are considered the archetyping namespace used in archetype multi-axial ids. For example, in openEHR, an archetype is identified by an id of the form `openEHR-EHR-OBSERVATION.some_obs.vN`. The 'EHR' in the above is an 'RM package closure', i.e. the name of an RM package whose class closure by reachability provides the set of classes that may be archetyped in the 'EHR' namespace. The reason for this is somewhat subtle: consider that if you want archetypes based on classes defined directly within a package P, and you also define these archetypes that re-use other archetypes based on more basic types like openEHR's `CLUSTER` or similar (typically not defined in the head package), then you will inevitably have archetypes based on `CLUSTER` only for use with EHR archetypes e.g. archetypes based on `OBSERVATION`. However, you may well create archetypes based on `CLUSTER` only for use with e.g. top-level archetypes from the demographic package. The archetype_rm_closure_packages setting is used to define the root id namespaces for archetypes, allowing low-level archetypes to be designated for use with one or other high-level archetype. E.g. `openEHR-EHR-CLUSTER.bp_position.v1` would be used only with `openEHR-EHR-OBSERVATION.bp_measurement.vX`, or similar, and most likely not with any `openEHR-DEMOGRAPHIC-XXXX.yyyy.vN` archetype. Note that in openEHR, there is nothing to prevent such cross-namespace reuse - it is just a design guideline. This attribute, must be defined in the same schema that defines the referenced package(s).

[source, odin]
--------
------------------------------------------------------
-- archetyping
-- override archetype parent class from included schema
------------------------------------------------------
archetype_parent_class = <"CLASS_NAME">
archetype_data_value_parent_class = <"CLASS_NAME">
archetype_visualise_descendants_of = <"CLASS_NAME">
archetype_rm_closure_packages = <"org.openehr.test_pkg", ...>
--------


== Package Definition

The packages definition should be self-explanatory: just name the classes and packages in a recursive fashion.

NOTE: only top-level package ids can be paths (i.e. contain the '.' character)

NOTE: only classes defined in the same schema can be referenced in the package section in that schema.

NOTE: make sure that the ODIN 'keys' are the same as the 'name' attributes in each block.

[source, odin]
--------
packages = <
    ["org.openehr.test_pkg"] = <
        name = <"org.openehr.test_pkg">
        classes = <"WHOLE", "SOME_TYPE", "BOOK", "CHAPTER", "ENTRY", "CAR", "CAR_BODY">
    >
>
--------

== Primitive Types

Definitions for primitive types in a BMM schema are just normal class definitions within a `primitive_types` block. Types that are included here are usually types corresonding to primitives in target programming languages, XML schema or other downstream technologies. These types can be be detected as primitive types by tools, but otherwise are processed in the same way as types defined in the main `class_definitions` group.

NOTE: unlike UML, all container types such as `List<T>`, `Hash<V,K>` etc are exlicit in a BMM schema, and consequently, such types are normally defined (including with generic parameters) in a BMM schema.

[source, odin]
--------
primitive_types = <
    ["Any"] = <
        name = <"Any">
        is_abstract = <True>
    >
    ["Ordered"] = <
        name = <"Ordered">
        is_abstract = <True>
        ancestors = <"Any", ...>
    >
>
--------

== Class Definitions

The main group of class definitions in a schema occurs within the `_class_definitions_` block. Each definition is a keyed ODIN object block correspnding to a serialised `P_BMM_CLASS` object, where the key is the class name. Since `name` is a BMM meta-model attribute, the class definition always contains its ODIN key. 

The possible class-level meta-properties: 

* `_name_` - class name - any capitalisation can be used, usually one of CamelCase or so-called UPPER_SNAKE_CASE;
* `_ancestors_` - states classes from which this class inherits, as an ODIN String list;
* `_is_abstract_` - indicates that the class cannot be instantiated directly;
* `_properties_` - ODIN block containing definitions consisting of `P_BMM_PROPERTY` descendants, keyed by property name.

=== Simple Classes

Simple classes are those whose type is the same as the class, as opposed to generic classes and enumerated types (see below).

[source, odin]
--------
class_definitions = <
    ["ITEM"] = <
        name = <"ITEM">
        ancestors = <"Any", ...>
        is_abstract = <True>
        properties = <
            -- properties here
        >
    >
    -- more classes here
>
--------

==== Class properties

Class properties from the original model are expressed using ODIN object blocks keyed by property name. Since there are multiple possible descendants of `P_BMM_PROPERTY`, ODIN type markers must be used to indicate which subtypes is used in each case.

The possible BMM meta-properties of all property types are as follows:

* `_name_` - `String` name of the property in its owning class in the model - use camelCase or snake_case as appropriate;
* `_is_mandatory_` - `Boolean` flag indicating whether the property is mandatory within its class.

The following shows the class `ELEMENT` with two properties `_null_flavour_` and `_value_` of BMM meta-type `P_BMM_SINGLE_PROPERTY`, i.e. corresponding to a single-valued attribute from the original model. 

[source, odin]
--------
    ["ELEMENT"] = <
        name = <"ELEMENT">
        ancestors = <"ITEM", ...>
        properties = <
            ["null_flavour"] = (P_BMM_SINGLE_PROPERTY) <
                name = <"null_flavour">
                type = <"DV_CODED_TEXT">
                is_mandatory = <True>
            >
            ["value"] = (P_BMM_SINGLE_PROPERTY) <
                name = <"value">
                type = <"DATA_VALUE">
            >
        >
    >
--------

==== Container Properties

The following is a `P_BMM_CONTAINER_PROPERTY` definition for the model property `items: List<ITEM>` in the `ELEMENT` class. The type is expressed in the `_type_def_` part which indicates the type of the container, which must be defined elsewhere in the schema, typically in the primitive types. The optional `_cardinality_` meta-property indicates cardinality of the container, and is expressed as a ODIN range. The default is `|0..*|`.
    
[source, odin]
--------
    ["ELEMENT"] = <
        name = <"ELEMENT">
        ancestors = <"ITEM", ...>
        properties = <
            ["items"] = (P_BMM_CONTAINER_PROPERTY) <
                name = <"items">
                type_def = <
                    container_type = <"List">
                    type = <"ITEM">
                >
                cardinality = <|>=1|>
                is_mandatory = <True>
            >
        >
    >
--------


=== Generic Classes

Generic classes are those that have one or more substitutable generic type parameters. Such classes are therefore _type generators_, since actual types are formed by substitution of concrete types (typically simple classes) for the formal type parameters. The following example shows a generic class `Interval` with `generic_parameter_defs` of `T` which is constrained to conform to the type `Ordered`. This structure defineds the type `Interval<T->Ordered>`, with the same meaning as UML and programming languages supporting generic (templated) types.

Generic classes will normally contain one or more properties whose formal type is the generic type parameter, i.e. the `T` in this example, as is the case below where the model properties `_lower_` and `_upper_` are both declared to be of type `T`. This declaration necessitates the use of the BMM meta-type `P_BMM_SINGLE_PROPERTY_OPEN`.

[source, odin]
--------
    ["Interval"] = <
        name = <"Interval">
        ancestors = <"Any", ...>
        generic_parameter_defs = <
            ["T"] = <
                name = <"T">
                conforms_to_type = <"Ordered">
            >
        >
        properties = <
            ["lower"] = (P_BMM_SINGLE_PROPERTY_OPEN) <
                name = <"lower">
                type = <"T">
            >
            ["upper"] = (P_BMM_SINGLE_PROPERTY_OPEN) <
                name = <"upper">
                type = <"T">
            >
        >
    >
--------

Given the presence of generic classes in a BMM schema, derived generic types can be used as the type of properties in other classes, for which the BMM meta-type `P_BMM_GENERIC_PROPERTY` is used. The folowing example shows first a generic class `DV_INTERVAL` defined in a similar way to `Interval` above, and then a class `SOME_TYPE` containing the property `_qty_interval_attr_` whose model type is `DV_INTERVAL<DV_QUANTITY>`. In the latter type declaration, the `DV_INTERVAL` is the `_root_type_` and `DV_INTERVAL` the `_generic_parameter_`.

[source, odin]
--------
    ["DV_INTERVAL"] = <
        name = <"DV_INTERVAL">
        ancestors = <"Interval", "DATA_VALUE">
        generic_parameter_defs = <
            ["T"] = <
                name = <"T">
                conforms_to_type = <"DV_ORDERED">
            >
        >
    >

    ["SOME_TYPE"] = <
        name = <"SOME_TYPE">
        ancestors = <"Any", ...>
        properties = <
            ["qty_interval_attr"] = (P_BMM_GENERIC_PROPERTY) <
                name = <"qty_interval_attr">
                type_def = <
                    root_type = <"DV_INTERVAL">
                    generic_parameters = <"DV_QUANTITY">
                >
            >
        >
    >
--------

Type declarations can also be nested types, for example and container followed by a generic type. In the following the `_careProvider_` attribute is declared to be of model type `List<ResourceReference<Party>>`. Any level of type nesting is allowed.

[source, odin]
--------
    ["Patient"] = <
        name = <"Patient">
        ancestors = <"Any", ...>
        properties = <
            ["careProvider"] = (P_BMM_CONTAINER_PROPERTY) <
                name = <"careProvider">
                type_def = <
                    container_type = <"List">
                    type_def = (P_BMM_GENERIC_TYPE) <
                        root_type = <"ResourceReference">
                        generic_parameters = <"Party">
                    >
                >
                cardinality = <|>=0|>
            >
        >
    >
--------

The following property definition is based on the class `REFERENCE_RANGE`, and in this case, has a generic parameter type that is another generic type: `DV_INTERVAL<DV_QUANTITY>`. To express this, we use `generic_parameter_defs` instead of just `_generic_parameters_` to specify a type structure, rather than just a string type name. Note that `generic_parameter_defs` is a logical list in general, since there can always be more than one generic parameter, i.e. 'T', 'U' etc, even though it is most commonly just one. Accordingly, the usual ODIN keyed hash structure is used with each member being keyed by a generic parameter name, below `["T"]`.                                           

[source, odin]
--------
    ["REFERENCE_RANGE"] = <
        name = <"REFERENCE_RANGE">
        ancestors = <"Any", ...>
        generic_parameter_defs = <
            ["T"] = <
                name = <"T">
                conforms_to_type = <"DV_ORDERED">
            >
        >
        properties = <
            ["range"] = (P_BMM_SINGLE_PROPERTY) <
                name = <"range">
                type = <"DV_INTERVAL">
                is_mandatory = <True>
            >
        >
    >

    ["RANGE_OF_INTERVAL_OF_QUANTITY"] = <
        name = <"RANGE_OF_INTERVAL_OF_QUANTITY">
        ancestors = <"Any", ...>
        properties = <
            ["range"] = (P_BMM_GENERIC_PROPERTY) <
                name = <"range">
                type_def = <
                    root_type = <"REFERENCE_RANGE">
                    generic_parameter_defs = <
                        ["T"] = (P_BMM_GENERIC_TYPE) <
                            root_type = <"DV_INTERVAL">
                            generic_parameters = <"DV_QUANTITY">
                        >
                    >
                >
            >
        >
--------

The following example just does the same as the one above, but shows an (unrealistic) but legal case of multiple, mixed, nested generic parameters corresponding to the model property definition `_range_: REFERENCE_RANGE<DV_INTERVAL<DV_QUANTITY>, Integer, List<DV_QUANTITY>, List<DV_INTERVAL<DV_QUANTITY>>>`. The rules for expressing types is clearly illustrated:

* use 'type' for simple string type refs; use `_type_def_` for structure types;
* within `P_BMM_GENERIC_TYPE`, use `_generic_parameters_` for a list of string types;
* use `_generic_parameter_defs_` for a list of complex type references.

[source, odin]
--------
    ["CRAZY_TYPE"] = <
        name = <"CRAZY_TYPE">
        ancestors = <"Any", ...>
        properties = <
            ["range"] = (P_BMM_GENERIC_PROPERTY) <
                name = <"range">
                type_def = <
                    root_type = <"REFERENCE_RANGE">
                    generic_parameter_defs = <
                        ["T"] = (P_BMM_GENERIC_TYPE) <
                            root_type = <"DV_INTERVAL">
                            generic_parameters = <"DV_QUANTITY">
                        >
                        ["U"] = (P_BMM_SIMPLE_TYPE) <
                            type = <"Integer">
                        >
                        ["V"] = (P_BMM_CONTAINER_TYPE) <
                            type = <"DV_QUANTITY">
                            container_type = <"List">
                        >
                        ["W"] = (P_BMM_CONTAINER_TYPE) <
                            type_def = (P_BMM_GENERIC_TYPE) <
                                root_type = <"DV_INTERVAL">
                                generic_parameters = <"DV_QUANTITY">
                            >
                            container_type = <"List">
                        >
                    >
                >
            >
        >
    >
--------

=== Enumerated Types

In a BMM schema, enumerated types are treated as constrained forms of standard types with open ranges, currently only `Integer` and `String`. They are accordingly represented using class definitions of the meta-types `P_BMM_ENUMERATION_INTEGER` and `P_BMM_ENUMERATION_STRING` respectively. In either case, just names (`_items_names_` meta-property) or both names and values (`_item_values_` meta-property) can be specified.

The following example shows two variants of am enumerated type based on the `Integer` primitive type.

[source, odin]
--------
    ["PROPORTION_KIND"] = (P_BMM_ENUMERATION_INTEGER) <
        name = <"PROPORTION_KIND">
        ancestors = <"Integer", ...>
        item_names = <"pk_ratio", "pk_unitary", "pk_percent", "pk_fraction", "pk_integer_fraction">
    >

    ["PROPORTION_KIND_2"] = (P_BMM_ENUMERATION_INTEGER) <
        name = <"PROPORTION_KIND_2">
        ancestors = <"Integer", ...>
        item_names = <"pk_ratio", "pk_unitary", "pk_percent", "pk_fraction", "pk_integer_fraction">
        item_values = <0, 1001, 1002, 1003>
    >
--------

The following example shows two similar examples based on `String`.

[source, odin]
--------
    ["MAGNITUDE_STATUS"] = (P_BMM_ENUMERATION_STRING) <
        name = <"MAGNITUDE_STATUS">
        ancestors = <"String", ...>
        item_names = <"le", "ge", "eq", "approx_eq">
        item_values = <"<=", ">=", "=", "~">
    >

    ["NAME_PART"] = (P_BMM_ENUMERATION_STRING) <
        name = <"NAME_PART">
        ancestors = <"String", ...>
        item_names = <"FIRST", "MIDDLE", "LAST">
    >
--------

