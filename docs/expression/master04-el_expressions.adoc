= EL Expressions

== Overview

Formally, an openEHR EL expression is any of the following value-returning entities:

* _terminal entities_:
** _literals_;
** _constants_;
** _symbolic variables_;
** _function calls_;
* _predicates_:
** _defined() predicate_: special expression for determing validity of external variable;
* _complex expressions_ created from:
** _equality operator_: equality operator;
** _primitive operators_: arithmetic operators, relational operators, boolean operators;
** _collection operators_: logical quantification operators.

Each of these is described in detail in the following sub-sections.

EL is designed syntactically as a readable language while being underpinned by fully class-based functional semantics, which are supplied either by a minimal built-in core, and mostly by bindings to external computing context. In this way, syntax elements such as the '+' operator for the `Date` type, or the `for_all` universal quantifier operator may be mapped to underlying methods such as `Date._add_()` and `Container._for_all_()`.

EL is not intended for writing functions or classes, rather it is designed as a minimalist logic-based language in which expressions can be written, but whose programming is undertaken inside libraries in existing programming languages. Routine declarations in EL are the primary mechanism to connect external software libraries to the EL context. In EL, both functions and procedures are supported, and are collectively known as _routines_.

== Terminal Entities

This section describes the various kinds of terminal entities that may appear in EL expressions.

=== Literals

Literal values are expressed in the {openehr_odin}#_primitive_types[ODIN syntax], with the exception of `List<T>` and `Set<T>` which are distinguished in EL with specific types of brackets.

[cols="1,2,3",options="header"]
|=================================================================
|Type                   |Literal value                          |Notes

| `Boolean`             |`True`, `False`                        |
| `Integer`             |`10`, `-4`, `1024`                     |
| `Real`                |`10.0`, `0.345`                        |
| `Date`                |`2004-08-12`                           |ISO8601-format date
| `Date_time`           |`2004-08-12T12:00:59+0100`             |ISO8601-format date/time
| `Time`                |`12:00:59`                             |ISO8601-format time
| `Duration`            |`P39W`                                 |ISO8601-format duration
| `String`              |`"this is a string"`                   |
|                       |`[blood_pressure]`                     |Extract the string value for the symbol `blood_pressure` from the EL terminology
| `Uri`                 |`https://en.wikipedia.org/wiki/Everest`|Uri in {rfc3986}[IETF RFC3986] format
| `Terminology_code`    |`[snomed_ct::389086002]` +
                         `[snomed_ct::389086002\|Hypoxia\|]`    |Terminology code in openEHR format


| `List<T>`             |`[val, val, ...]`                      |
| `Set<T>`              |`{val, val, ...}`                      |
| `Hash<K:Ordered, V>`  |`< +
                              ["key1"] = <val1> +
                              ["key2"] = <val2> +
                              ... +
                              ["keyN"] = <valN> +
                         >`                                     |A table of values of any type V, +
                                                                 keyed by values of any `Ordered` descendant K, +
                                                                 typically `String` or `Integer`

| `Interval<T>`         |                                       |
|                       |`\|N..M\|`                             |the two-sided interval N >= x \<= M
|                       |`\|>N..M\|`                            |the two-sided interval N > x \<= M
|                       |`\|N..<M\|`                            |the two-sided interval N >= x < M
|                       |`\|<N\|`                               |the one-sided interval x < N
|                       |`\|>N\|`                               |the one-sided interval x > N
|                       |`\|\<=N\|`                             |the one-sided interval x \<= N
|                       |`\|>=N\|`                              |the one-sided interval x >= N
|                       |`\|N +/-M\|`                           |the two-sided interval of N ±M
|                       |`\|N±M\|`                              |the two-sided interval of N ±M
|=================================================================

Literal values may be used in EL expressions, as follows.

----
    $kmh := 1.6 * $mph
----

=== Constants

Symbolic constants may be used in any EL expression. As with variables, constants are typed, and are either declared explicitly in the EL text (see <<_declarations, next section>>), or assumed to be available in the execution environment. Constants are syntactically represented using labels of whch the first letter is capitalised, and may be of any type, including complex types. The following are EL statements using constants.

--------
    $kmh := Mph_to_kmh_factor * $mph
    Safe_glucose_limits.has ($glucose_meas)
--------

=== Variables

Symbolic variables are of two types: local and external. Local variables are represented by plain names such as `is_smoker`, and are typically used as intermediate variables within a larger set of statements that generates some final value.

External variables represent data entities in the operational data context, and are either assumed from elsewhere in the computational context, or else declared in a `data_context` section (see <<_declarations, next section>>).

Typical expressions containing variables are as follows.

----
    $glucose < 6.5                          -- an external variable in a Boolean expression
    
    age := current_date() - $date_of_birth  -- age is a local variable in an assignment statement
----

=== Function Calls

In EL expressions, computational functions may be called in the same way as for typical programming languages. The following example shows a declaration for a function `tnm_major_number()` that extracts various elements of {wikipedia}/TNM_staging_system[Tumour/Node/Metastasis ('TNM')] cancer staging values, such as `'Tis'`, `'G3'` and so on, and uses them in a Boolean expression whose value is assigned to the variable `is_luminal_A`.

----
    func tnm_major_number (tnm_val: String): Integer

    is_luminal_A := tnm_major_number ($tnm_t) < 3 and 
                     tnm_major_number ($tnm_n) < 2 and 
                     tnm_major_number ($tnm_g) < 3
----

To be evaluated, function calls must be mappable to class methods in external libraries that are available at EL statement execution time.

=== Built-in Functions

Some common functions are assumed to be provided in an EL environment, in order to enable EL expressions to be more standardised. These can be provided by the implementation using the above methods of declaration and external binding.

Some built-in functions are listed below.

----
    current_date(): Date                   -- obtain today's date
    current_time(): Time                   -- obtain the current clock time
    current_date_time(): Date_time         -- obtain the current date and time
    
    sum (Container<T: Numeric>): T         -- compute the sum of members in a container
    min (Container<T: Numeric>): T         -- compute the minimum value in a container
    max (Container<T: Numeric>): T         -- compute the maximum value in a container
    avg (Container<T: Numeric>): T         -- compute the average value in a container

    count (Container<T>): Integer          -- compute the number of items in a container
----

These functions operate as a shorthand for underlying object-oriented calls defined on various kinds of objects, and would be implemented by such mappings. For example, `_current_date_()` might be mapped to a `_now_()` function on the type `Date`. The statistical functions `_sum_()` etc might be mapped to functions of similar names on types such as `Container_numeric`, derived from `Container<T: Numeric>`.

== Predicates

EL predicates are special meta-operators that enable execution to be modified depending on the truth values and availability of referenced data items.

=== defined() Predicate

Variables that are bound to entities in the data context function differently from local variables, since their availability is predicated on the existence of the relevant entities. For example, the variable `body_weight` may be bound to a call that retrieves a patient weight from the EHR, via an appropriate API call. There is no guarantee that the value is available, so `body_weight` may therefore be undefined in a sense not applicable to local variables. In a programming language, if a variable is not explicitly set, it has either the default value of the type (e.g. `0` for `Integer`) or a random value of the correct type. This behaviour is appropriate for local variables, but for external variables that cannot be evaluated because the external entity does not exist, an explicit mechanism is needed to test for validity.

The approach used for EL is to allow external variables to be used freely, as for local variables, but if an external variable cannot be evaluated from the data context, an `'undefined value'` exception is generated, indicating which variable could not be evaluated. The `defined()` predicate provides a way of making an explicit check to avoid an exception, as follows.

----
    if defined ($heart_rate) and defined ($blood_pressure) then
        -- statements mentioning $heart_rate and $blood_pressure
    end
----

Another difference between external and local variables is when they are evaluated. In the execution of a larger EL Module containing multiple mentions of an external variable `$v`, is `$v` read from the data context only once, at the first mention, or is it evaluated new each time, or on some other basis? The approach used in EL is to allow the evaluation basis to be stated in the context binding section of an EL Module using the `_currency_` property.

== Complex Expressions

Complex expressions in EL consist of non-atomic value-returning expressions, in a familiar typed, operator-based syntax common to many programming languages and logics. In EL, the syntactic use of operators is understood as a shorthand for certain functions assumed to be available on certain types, and an EL implementation would map such operators to the appropriate methods in a class library.

=== Equality Operator

The equality operator `=` is special in EL as in most languages, and has two meanings. For all primitive types, the semantics are value comparison, while for container and other non-primitive types, the semantics are reference comparison.

=== Primitive Operators

Primitive operators in EL are the infix or prefix syntax form of various functions available on primitive types. For example, the operator `-` (minus) is defined on the class `Numeric` (an inheritance ancestor of the classes `Integer`, `Real` etc) as the following (using syntax from Java, TypeScript and similar languages):

[source,java]
----
    T function minus<T extends Numeric> (T other)
----

This means that where the expression `100 - 5` is encountered in EL, what is really invoked is `Integer._minus_()`, specifically `100.minus(5)`.

The operators for Numeric and Boolean types supported in EL are shown below.

[cols="1,1,1,3",options="header"]
|=================================================================
|Identifier   |Textual +
               Rendering 	  |Symbolic +
                               Rendering  |Meaning
                               
4+^h|Arithmetic Operators - Numeric operands and result; descending precendence order

|exp          | ^             |^          |Exponentiation
|times        | *             |*          |Multiplication
|divide       | /             |/          |Division
|mod          | %             |%          |Modulo (whole number) division
|plus         | +             |+          |Addition
|minus        | -             |-          |Subtraction

4+^h|Relational Operators - Numeric, Date/time operands and Boolean result; equal precedence

|eq           | =             |=          |Value equality
|ne           | !=            |≠          |Inequality relation
|lt           | <             |<          |Less than relation
|le           | \<=           |≤          |Less than or equal relation
|gt           | >             |>          |Greater than relation
|ge           | >=            |≥          |Greater than or equal relation

4+^h|Logical Operators - Boolean operands and result; descending precendence order

|not          |not, ~ 		|∼  		 |Negation, "not p"
|and          |and 			|∧ 			|Logical conjunction, "p and q"
|or           |or 			|∨ 			|Logical disjunction, "p or q"
|xor          |xor 			|⊻ 			 |Exclusive or, "only one of p or q"
|implies      |implies 		|⇒ 			 |Material implication, "p implies q", or "if p then q"

4+^h|Constraint Operators - Boolean result

|matches      |matches 		|∈  		 |Set membership, as per {openehr_am_adl2}[openEHR ADL]

|=================================================================

In addition, some operators are defined on the other primitive types, corresponding to underlying functions defined on the relevant types. These are as follows:

[cols="1,2,4",options="header"]
|=================================================================
|Operator       |Typical +
                 method    	                                    |Meaning
                               
3+^h|String Operators

|`+`             |`append(other: String)`                       |String concatenation, appending

3+^h|Date/time arithmetic operators

|`+`             |`Date.add(d: Duration): Date`                 |Add a duration to a date
|`+`             |`Time.add(d: Duration): Time`                 |Add a duration to a time
|`+`             |`Date_time.add(d: Duration): Date_time`       |Add a duration to a date/time
|`+`             |`Duration.add(d: Duration): Duration`         |Add a duration to a duration

|`-`             |`Date.subtract(d: Duration): Date`            |Subtract a duration from a date
|`-`             |`Time.subtract(d: Duration): Time`            |Subtract a duration from a time
|`-`             |`Date_time.subtract(d: Duration): Date_time`  |Subtract a duration from a date/time
|`-`             |`Duration.subtract(d: Duration): Duration`    |Subtract a duration from a duration

|`-`             |`Date.diff(d: Date): Duration`                |Difference of two dates
|`-`             |`Time.diff(d: Time): Duration`                |Difference of two times
|`-`             |`Date_time.diff(d: Date_time): Duration`      |Difference of two date/times

3+^h|Duration arithmetic operators

|`+`             |`Duration.add(d: Duration): Duration`         |Add a duration to a duration
|`-`             |`Duration.subtract(d: Duration): Duration`    |Subtract a duration from a duration

|=================================================================

Operator semantics that require further explanation are described below.

==== Logical Negation

All Boolean operators take Boolean operands and generate a Boolean result. The `not` operator can be applied as a prefix operator to all operators returning a Boolean result as well as a parenthesised Boolean expression.

==== Precedence and Parentheses

The precedence of operators follows the order shown in the operator tables above. To change precedence, parentheses can be used in the fashion typical of most programming languages, as shown below.

--------
    at_risk := $systolic_bp > 140 and ($is_smoker or $is_hypertensive)
--------

=== Collection Operators

A common need in an expression language featuring container types (i.e. sets, lists etc) is the need to perform basic reasoning over them. The two standard operators from predicate logic `there exists` (∃ operator) and `for all` (∀ operator) are defined in EL for the container types, and are kinds of Boolean-returning expressions.

The syntax of `there exists` is as follows:

----
    there_exists v in container_var : <Boolean expression mentioning v> 
----

Here, the `:` symbol is usually read in English as 'such that'. The expression may be used as in the following example:

----
    if there_exists v in $systolic_bp_samples : v > Systolic_bp_threshold then
        -- statements
    end
----

The `for_all` operator has similar syntax:

----
    for_all v in container_var : <Boolean expression mentioning v>
----

Here, the `:` symbol is normally read as 'it holds that' A typical use is as follows:

----
    if for_all v in $systolic_bp_samples : v <= Systolic_bp_threshold then
        -- statements
    end
----

The syntactic form of these two operators can be understood as a shorthand for the following underlying functions defined on the container types, available in most programming language libraries. The argument to both functions is a function object with signature `<v:T>: Boolean`.

----
    there_exists (test(v: T): Boolean): Boolean
            -- True if there is any v in container for which test (v) is True

    for_all (test(v: T): Boolean): Boolean
            -- True if for every v in container, test (v) is True
----
